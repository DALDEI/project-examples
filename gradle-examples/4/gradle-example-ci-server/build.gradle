/*
 * Copyright (C) 2013 JFrog Ltd.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
  repositories {
    jcenter()
  }
}

allprojects {
  repositories {
      jcenter() 
  }
  apply plugin: 'java'
  apply plugin: 'maven'

  group = 'org.jfrog.example.gradle'
/* use currentVersion from gradle.properties
 *  configure CI to update this property on release build
 *  TeamCity Relase Management Gradle Plugin example  
 *  set both 'Release properties:' and 'Next Integration Properties'
 *  to 'currentVersion'
 */
  version = currentVersion
  // Maven style integration status 
  if( version.endsWith("-SNAPSHOT" ) )
    status = 'integration'
  else
    status = 'release'
}
 
// Services is a war project without its own build.gradle
project(':services') {
  apply plugin: 'war'
}

/* Setting the task 'artifactoryPublish' property 'skip to 'true'
 * to true will make the artifactoryPublish task  skip this module 
 * (in our case, the root module): Otherwise a jar file with a 
 * manifest only will be published.
 * If running standalone without the artifactory plugin applied 
 * you cannot refernce the property directly withhout error.
 * the task is created very late in the lifecycle so wait until
 * the root project is evaluted then check if the artifactoryPublish
 * task exists - only then set the task property 'skip' to "true"
 */
afterEvaluate  { p ->
  Task t = p.tasks.findByPath(":artifactoryPublish") 
  if( t != null ){
      println "HAS artifactoryPublish task -- setting 'skip' to 'true'"
      t.setProperty("skip","true")
  } else {
     // Running without artifcatory -- need a resolving repository
     println "DOES NOT HAVE artifactoryPublish -- adding jcenter repository"
     rootProject.allprojects {
       repositories { 
         jcenter() 
       }
    }
  }
}
  

task wrapper(type: Wrapper) {
  gradleVersion = '2.11' // modern version 
}
